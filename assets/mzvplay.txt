set "$file" to "out.mzv"
mod "out.ogg"

set "mzx_speed" to 2
set "commands" to 10000
playercolor is c00
color c00
. "This audio synchronization wait is configuration and video dependent :("
. "wait for 4"
set "&$file&" to "fread_open"
goto "#play"
set "" to "fread_open"
end mod
end

. "Player will track up to time_m units of missed frames."
: "#time"
set "time_n" to "('time_seconds'*1000+'time_millis')"
set "time_d" to 1000
set "time_m" to "(60000)"
if "('time_leap' ? 'time_n'<'time_m' : 0)" = 1 then "leap"
if "time_n" < "time_m" then "#return"
set "time_leap" to 1
goto "#return"
: "leap"
dec "time_prev" by "time_d"
set "time_leap" to 0
goto "#return"

: "#vlayer"
set "vlayer_size" to "('vw'*'vh')"
set "vlayer_width" to "vw"
. "Sprite reference offsets."
set "vx" to 0
set "vy" to 0
set "vo" to 256
. "Current sprite index."
set "idx" to 0
goto "#return"

: "#sprite"
set "spr&idx&_overlaid" to 1
goto "#return"


. "----------------------------------------------------------------------"


: "#play"
set "$" to "fread4"
if "$" === "MZXV" then "play"
: "err"
goto "#top"
: "play"
set "local" to "fread_counter"
set "time_n" to 0
set "time_d" to 1000
goto "#time"
set "time_prev" to "time_n"
set "w" to 0
set "h" to 0
set "vw" to 0
set "vh" to 0
set "sp_x" to 0
set "sp_y" to 0
set "sp_w" to 1
set "sp_h" to 1
set "sp_u" to 0
set "sp_o" to 0
set "sp_t" to 0
set "mode" to 0
set "fr_n" to 125
set "fr_d" to 6
set "fr_adv" to 0
set "$chr" to ""
set "$pal" to ""
set "$idx" to ""
set "$mzm" to "MZM2"
set "$mzm.8#4" to 0
set "$mzm.12#4" to 0
set "$mzm.13" to 1
: "headers"
goto "#chunk"
goto "head:&$&"
if "w" = 0 then "err"
if "h" = 0 then "err"
set "vw" to "('vw' ? 'vw' : 'w')"
set "vh" to "('vh' ? 'vh' : 'h')"
goto "#vlayer"
set "$mzm.4#2" to "w"
set "$mzm.6#2" to "h"
set "local" to "('sp_w'*'sp_h'-1)"
set "local4" to "('sp_u'o'vo'o'sp_o' ? 1 : 0)"
set "local2" to "(('local4' ? -'w'*8+640/2 : 80-'w'/2) + 'sp_x')"
set "local3" to "(('local4' ? -'h'*14+350/2 : 25-'h'/2) + 'sp_y')"
loop start
set "spr&idx&_width" to "w"
set "spr&idx&_height" to "h"
set "spr&idx&_refx" to "('loopcount'%'sp_w'*'w'+'vx')"
set "spr&idx&_refy" to "('loopcount'/'sp_w'*'h'+'vy')"
set "spr&idx&_vlayer" to 1
set "spr&idx&_unbound" to "local4"
set "spr&idx&_offset" to "('loopcount'*'sp_o'+'vo')"
set "spr&idx&_tcol" to "('loopcount' ? 'sp_t' : -1)"
put c?? Sprite "idx" at "('loopcount'%'sp_w'+'local2')" "('loopcount'/'sp_w'+'local3')"
goto "#sprite"
inc "idx" by 1
loop for "local"
dec "fread_pos" by 8
: "frames"
goto "#chunk"
goto "frame:&$&"
. "fallthrough"
: "frame_wait"
inc "fr_total" by 1
dec "fr_adv" by 1
if "fr_adv" > 0 then "frames"
: "w"
wait for 1
goto "#frameadv"
if "fr_adv" > 0 then "frames"
goto "w"

: "#frameadv"
set "fr_adv" to 0
goto "#time"
set "local" to "('time_n'-'time_prev')"
inc "local" by "('local'<0 ? 'time_m' : 0)"
set "local2" to "('local'/'time_d')"
set "local3" to "('local'%'time_d')"
set "local4" to "('local3'*'fr_n'/'time_d')"
inc "local4" by "('local2'*'fr_n')"
set "fr_adv" to "('local4'/'fr_d')"
if "fr_adv" <= 0 then "#return"
. "Only consume the amount of time it takes to play exactly fr_adv frames."
set "local6" to "('fr_adv'*'time_d'*'fr_d'/'fr_n'-'local')"
set "time_prev" to "('time_n'+'local6')"
goto "#return"

: "#chunk"
set "$" to "fread4"
set "sz" to "fread_counter"
if "sz" < 0 then "err"
goto "#return"

: "head:fwid"
if "sz" < 4 then "err"
set "w" to "fread_counter"
if "w" < 0 then "err"
if "w" > 32767 then "err"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:fhei"
if "sz" < 4 then "err"
set "h" to "fread_counter"
if "h" < 0 then "err"
if "h" > 32767 then "err"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:bwid"
if "sz" < 4 then "err"
set "vw" to "fread_counter"
if "vw" < 0 then "err"
if "vw" > 32767 then "err"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:bhei"
if "sz" < 4 then "err"
set "vh" to "fread_counter"
if "vh" < 0 then "err"
if "vh" > 32767 then "err"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:sprx"
if "sz" < 4 then "err"
set "sp_x" to "fread_counter"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:spry"
if "sz" < 4 then "err"
set "sp_y" to "fread_counter"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:sprw"
if "sz" < 4 then "err"
set "sp_w" to "fread_counter"
if "sp_w" < 0 then "err"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:sprh"
if "sz" < 4 then "err"
set "sp_h" to "fread_counter"
if "sp_h" < 0 then "err"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:spru"
if "sz" < 4 then "err"
set "sp_u" to "fread_counter"
if "sp_u" < 0 then "err"
if "sp_u" > 1 then "err"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:spro"
if "sz" < 4 then "err"
set "sp_o" to "fread_counter"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "head:sprt"
if "sz" < 4 then "err"
set "sp_t" to "fread_counter"
if "sp_t" < -1 then "err"
if "sp_t" >= 256 then "err"
inc "fread_pos" by "('sz'-4)"
goto "headers"

: "frame:smzx"
if "sz" < 4 then "err"
set "mode" to "fread_counter"
if "mode" < 0 then "err"
if "mode" > 3 then "err"
inc "fread_pos" by "('sz'-4)"
set "smzx_mode" to "mode"
load palette "$pal"
if "mode" != 3 then "frames"
set "$idx" to "smzx_indices"
goto "frames"

: "frame:rate"
if "sz" < 8 then "err"
set "fr_n" to "fread_counter"
set "fr_d" to "fread_counter"
if "fr_n" < 0 then "err"
if "fr_d" <= 0 then "err"
inc "fread_pos" by "('sz'-8)"
goto "frames"

: "frame:fchr"
set "$chr" to "fread&sz&"
load char set "@&vo&$chr"
goto "frames"

: "frame:fpal"
set "$pal" to "fread&sz&"
load palette "$pal"
goto "frames"

: "frame:fidx"
set "$idx" to "fread&sz&"
set "$idx" to "smzx_indices"
goto "frames"

: "frame:f1ch"
set "$" to "fread&sz&"
set "local" to "('w'*'h'-1)"
loop start
set "$mzm.('loopcount'<<1+16)" to "$.&loopcount&"
loop for "local"
put "$@mzm" Image_file p02 at "vx" "vy"
goto "frame_wait"

: "frame:f1co"
set "$" to "fread&sz&"
set "local" to "('w'*'h'-1)"
loop start
set "$mzm.('loopcount'<<1+17)" to "$.&loopcount&"
loop for "local"
put "$@mzm" Image_file p02 at "vx" "vy"
goto "frame_wait"

: "frame:f2in"
set "$mzm+16" to "fread&sz&"
set "$mzm.length" to "('w'*'h'*2+16)"
put "@$mzm" Image_file p02 at "vx" "vy"
goto "frame_wait"
